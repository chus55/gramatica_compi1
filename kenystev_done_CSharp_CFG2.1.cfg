compilation-unit:
	| optional-using-directive optional-namespace-member-declaration
	| optional-namespace-member-declaration
    | EPSILON

optional-using-directive:
	| using-directive
	| EPSILON

optional-namespace-member-declaration:
	| namespace-member-declaration
	| EPSILON

using-directive:
	| "using" identifier identifier-attribute ';' optional-using-directive

namespace-member-declaration:
	| namespace-declaration optional-namespace-member-declaration
	| type-declaration-list


identifier-attribute:
	| '.' identifier identifier-attribute
	| EPSILON

type-declaration-list:
	| type-declaration type-declaration-list
	| EPSILON

namespace-declaration:
	| "namespace" identifier identifier-attribute namespace-body

type-declaration:
	| encapsulation-modifier group-declaration

encapsulation-modifier:
	| "public"
	| "protected"
	| "private"
	| EPSILON

namespace-body:
	| '{' optional-using-directive optional-namespace-member-declaration '}'

group-declaration:
	| class-declaration
    | interface-declaration
    | enum-declaration

class-declaration:
	| class-modifier "class" identifier inheritance-base class-body optional-body-end

interface-declaration: 
	| "interface" identifier inheritance-base interface-body optional-body-end

enum-declaration: 
	| "enum" identifier enum-body optional-body-end

inheritance-base:
	| ':' identifiers-list
	| EPSILON

class-body:
	| '{' optional-class-member-declaration-list '}'

optional-class-member-declaration-list:
	| class-member-declaration optional-class-member-declaration-list
	| EPSILON

optional-body-end:
	| ';'
	| EPSILON

identifiers-list:
	| identifier identifiers-list-p

identifiers-list-p:
	| ',' identifier identifiers-list-p
	| EPSILON

enum-body:
	| '{' optional-assignable-identifiers-list '}'

optional-assignable-identifiers-list:
	| identifier assignment-options
	| EPSILON

assignment-options:
	| optional-assignable-identifiers-list-p
	| '=' expression optional-assignable-identifiers-list-p
	| EPSILON

optional-assignable-identifiers-list-p:
	| ',' optional-assignable-identifiers-list
	| EPSILON

interface-body:
	| '{' interface-method-or-constructor-declaration-list '}'

interface-method-or-constructor-declaration-list:
	| interface-method-header ';' interface-method-or-constructor-declaration-list
	| EPSILON

interface-method-header:
	| type-or-void identifier '(' fixed-parameters ')' 

fixed-parameter:
	| type identifier

fixed-parameters:
	| fixed-parameter fixed-paramaters-p
	| EPSILON

fixed-parameters-p:
	| ',' fixed-parameter fixed-parameters-p
	| EPSILON

class-member-declaration: 
	| encapsulation-modifier class-member-declaration-options

; SEMANTIC: validar que el constructor no tenga tipo de retorno
class-member-declaration-options:
    | optional-modifier type-or-void identifier field-or-method-or-constructor

; SEMANTICO: Validar que static solo puede estar en field. Virtual, override y abstract, unicamente en method.
optional-modifier:
    | "static"
    | "virtual"
    | "override"
    | "abstract"
    | EPSILON

field-or-method-or-constructor:
    | field-declaration
	| method-or-constructor-declaration

; SEMANTIC: Validar que si la clase es abstract, el metodo no debe llevar cuerpo.
; SEMANTIC: solo un constructor puede llevar : "base" '(' argument-list ')'
method-or-constructor-declaration:
	| '(' fixed-parameters ')' constructor-initializer maybe-empty-block

field-declaration: 
	| variable-assigner variable-declarator-list-p ';'

variable-declarator-list:
	| identifier variable-assigner variable-declarator-list-p

variable-declarator-list-p:
	| ',' variable-declarator-list
	| EPSILON

constructor-initializer:
	| ':' "base" '(' argument-list ')'
	| EPSILON

variable-assigner:
	| '=' variable-initializer
	| EPSILON

variable-initializer:
	| expression
	| array-initializer

array-initializer: 
	| '{' optional-variable-initializer-list '}'

optional-variable-initializer-list:
	| variable-initializer-list
	| EPSILON

variable-initializer-list:
	| variable-initializer variable-initializer-list-p

variable-initializer-list-p:
	| ',' variable-initializer-list
	| EPSILON

maybe-empty-block:
	| '{' optional-statement-list '}'
	| ';'


argument-list:
	| expression argument-list-p
	| EPSILON

argument-list-p:
	| ',' expression argument-list-p
	| EPSILON

optional-statement-list:
	| statement-list
	| EPSILON

statement-list: 
	| statement optional-statement-list 

statement:
	| local-variable-declaration ';'
	| embedded-statement

local-variable-declaration:
	| type-or-var variable-declarator-list	

embedded-statement:
	| maybe-empty-block
	| statement-expression ';'
	| selection-statement
	| iteration-statement
	| jump-statement ';'

expression:
    | conditional-expression

conditional-expression:
	| null-coalescing-expression conditional-expression-p

conditional-expression-p:
	| '?' expression ':' expression 
	| EPSILON

null-coalescing-expression:
	| conditional-or-expression null-coalescing-expression-p

null-coalescing-expression-p:
	| "??" null-coalescing-expression
	| EPSILON

conditional-or-expression:
	| conditional-and-expression conditional-or-expression-p

conditional-or-expression-p:
	| "||" conditional-and-expression conditional-or-expression-p 
	| EPSILON

conditional-and-expression:
	| inclusive-or-expression conditional-and-expression-p

conditional-and-expression-p:
	| "&&" inclusive-or-expression conditional-and-expression-p
	| EPSILON 

inclusive-or-expression:
	| exclusive-or-expression inclusive-or-expression-p

inclusive-or-expression-p:
	| "|" exclusive-or-expression inclusive-or-expression-p
	| EPSILON

exclusive-or-expression:
	| and-expression exclusive-or-expression-p

exclusive-or-expression-p:
	| "^" and-expression exclusive-or-expression-p
	| EPSILON 

and-expression:
	| equality-expression and-expression-p

and-expression-p:
	| "&" equality-expression and-expression-p
	| EPSILON

equality-expression:
	| relational-expression equality-expression-p

equality-expression-p:
	| expression-equality-operator relational-expression equality-expression-p
	| EPSILON

relational-expression:
	| shift-expression relational-expression-p

relational-expression-p:
	| expression-relational-operator shift-expression relational-expression-p
	| is-as-operators type relational-expression-p
	| EPSILON 

shift-expression:
	| additive-expression shift-expression-p

shift-expression-p:
	| expression-shift-operator additive-expression shift-expression-p
	| EPSILON

additive-expression:
	| multiplicative-expression additive-expression-p

additive-expression-p:
	| additive-operators multiplicative-expression additive-expression-p
	| EPSILON

multiplicative-expression:
	| unary-expression multiplicative-expression-factorized

multiplicative-expression-factorized:
	| assignment-operator expression multiplicative-expression-p
	| multiplicative-expression-p

multiplicative-expression-p:
	| multiplicative-operators unary-expression multiplicative-expression-p
	| EPSILON

unary-expression:
	| expression-unary-operator unary-expression
	| '(' type ')' primary-expression
	| primary-expression

primary-expression:
    | "new" instance-expression primary-expression-p
	| literal primary-expression-p
	| identifier primary-expression-p
	| '(' expression ')' primary-expression-p
	| "this" primary-expression-p

primary-expression-p:
    | '.' identifier primary-expression-p
    | optional-funct-or-array-call primary-expression-p
    | increment-decrement primary-expression-p 
    | EPSILON

optional-funct-or-array-call:
	| '(' argument-list ')'
	| optional-array-access-list
    | EPSILON 

instance-expression:
	| type instance-expression-factorized



optional-array-access-list:
	| '[' expression-list ']' optional-array-access-list
	| EPSILON

expression-list:
	| expression optional-expression-list

optional-expression-list:
	| ',' expression-list
	| EPSILON

instance-expression-factorized:
	| '[' instance-expression-factorized-p 
	| '(' argument-list ')'

instance-expression-factorized-p:
	| expression-list ']' optional-rank-specifier-list optional-array-initializer
	| rank-specifier-list array-initializer

optional-array-initializer:
	| array-initializer
	| EPSILON

rank-specifier-list: 
	| rank-specifier optional-rank-specifier-list

optional-rank-specifier-list:
	| '[' rank-specifier-list
	| EPSILON 	

rank-specifier:
	| optional-comma-list ']'

optional-comma-list:
	| ',' optional-comma-list
	| EPSILON

statement-expression:
	| unary-expression statement-expression-factorized

statement-expression-factorized:
	        | assignment-operator expresion statement-expresion-p
            | statement-expresion-p

statement-expression-p:
	| '(' argument-list ')'
	| increment-decrement
    | EPSILON

optional-unary-expression:
    | expression-unary-operator unary-expression
    | '(' type ')'
    | EPSILON

jump-statement:
	| "break"
	| "continue"
	| "return" optional-expression

optional-expression: 
	| expression
	| EPSILON

iteration-statement:
	| while-statement
	| do-statement
	| for-statement
	| foreach-statement

while-statement:
	| "while" '(' expression ')' embedded-statement

do-statement: 
	| "do" embedded-statement "while" '(' expression ')' ';'

for-statement:
	| "for" '(' optional-for-initializer ';' optional-expression ';' optional-statement-expression-list ')' embedded-statement

foreach-statement:
	| "foreach" '(' type-or-var identifier "in" expression ')' embedded-statement

optional-for-initializer:
	| local-variable-declaration
	| statement-expression-list
	| EPSILON

optional-statement-expression-list:
	| statement-expression-list
	| EPSILON

statement-expression-list:
	| statement-expression statement-expression-list-p

statement-expression-list-p:
	| ',' statement-expression statement-expression-list-p
	| EPSILON

selection-statement:
	| if-statement
	| switch-statement

if-statement:
	| "if" '(' expression ')' embedded-statement optional-else-part

optional-else-part:
	| else-part
	| EPSILON

else-part:
	| "else" embedded-statement	

switch-statement:
	| "switch" '(' expression ')' '{' optional-switch-section-list '}'

optional-switch-section-list:
	| switch-label-list optional-statement-list optional-switch-section-list
	| EPSILON

switch-label-list:
	| switch-label switch-label-list-p

switch-label-list-p:
	| switch-label-list
	| EPSILON
	
switch-label:
	| "case" expression ':'
	| "default" ':'

qualified-identifier:
	| identifier identifier-attribute

identifiers-list:
    | qualified-identifier identifiers-list-p

identifiers-list-p:
    | ',' qualified-identifier identifiers-list-p
    | EPSILON

namespace-declaration:
    | "namespace" qualified-identifier namespace-body

using-directive:
    | "using" qualified-identifier ';' optional-using-directive

type:
    | built-in-type optional-rank-specifier-list
    | qualified-identifier optional-rank-specifier-list

built-in-type:
	| "int"
	| "char"
	| "string"
	| "bool"
	| "float"

type-or-var:
	| type
	| "var"

type-or-void:
	| type
	| "void"