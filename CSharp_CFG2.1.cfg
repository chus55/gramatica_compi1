;	Consideraciones: 
;		1. Todos las producciones "optional" deben tener una produccion "EPSILON"
;		2. __(Done)__ MÃºltiples clases, interfaces, enums fuera de namespace 

code:
	| compilation-unit 

{code.valor = compilation-unit.node}

compilation-unit: #Done
	| optional-using-directive (1) optional-namespace-member-declaration (2)
	| optional-namespace-member-declaration (3)

-1 {using-node[] = optional-using-directive[]}
-2 {namespace-node[] = optional-namespace-member-declaration[]}
-3 {namespace-node[]= optional-namespace-member-declaration[]}

optional-using-directive: #DONE
	| using-directive (1)
	| EPSILON (2)

-1 {optional-using-directive.valor = using-directive(optional-using-directive.param?? new using-node[])}
-2 {optional-using-directive.valor = optional-using-directive.param}

using-directive: #DONE
	| "using" identifier identifier-attribute ';' (1) optional-using-directive (2)

-1 {using-directive.param.add(new using-node())}
-2 {using-directive.valor = optional-using-directive(using-directive.param).valor}

optional-namespace-member-declaration: #DONE
	| namespace-member-declaration (1) 
	| EPSILON (2)

-1 {optional-namespace-member-declaration.valor = namespace-member-declaration(optional-namespace-member-declaration.param?? new namespace-node() )}
-2 {optional-namespace-member-declaration.valor = optional-namespace-member-declaration.param}

namespace-member-declaration:#DONE
	| namespace-declaration (1) optional-namespace-member-declaration (2)
	| (3) type-declaration-list optional-namespace-member-declaration (4)

-1 { namespace-member-declaration.param.add(namespace-declaration()) }
-2 { namespace-member-declaration.valor = optional-namespace-member-declaration(namespace-member-declaration.param)}
-3 {namespace-member-declaration.param.add(type-declaration())}
-4 (namespace-member-declaration.valor = optional-namespace-member-declaration(namespace-member-declaration.param))

type-declaration-list:#DONE
	| (1) type-declaration (2) type-declaration-list
	| EPSILON (3)

-1 {type-declaration(type-declaration-list.param?? new namespace-node())}
-2 {type-declaration-list.valor =  type-declaration-list(type-declaration-list.param)}
-3 {type-declaration-list.valor = type-declaration-list.param}

type-declaration:#DONE
	| encapsulation-modifier group-declaration (1)

-1 {type-declaration.valor = type-declaration.param.type-declaration[].add(group-declaration(encapsulation-modifier.valor))}

namespace-declaration:#DONE
	| "namespace" identifier identifier-attribute namespace-body (1)
 -1 {
 	var namesNode = new namespace-node(identifier.lexema);
 	namespace-body(ref namesNode)
 	return namesNode;
 }

namespace-body:#DONE
	| '{' optional-using-directive optional-namespace-member-declaration '}'
	
identifier-attribute:#DONE
	| '.' identifier identifier-attribute
	| EPSILON

group-declaration:
	| class-declaration (1)
    | interface-declaration (2)
    | enum-declaration (3)
-1 {group-declaration.valor = class-declaration(group-declaration.param)} 
-2 {group-declaration.valor = interface-declaration(group-declaration.param)}
-3 {group-declaration.valor = enum-declaration(group-declaration.param)}

class-declaration:
	| class-modifier "class" identifier inheritance-base class-body optional-body-end

#SEMANTIC: validar que no pueden haber private, ni protected enums afuera de un namespace.
encapsulation-modifier:#DONE
	| "public"
	| "protected"
	| "private"
	| EPSILON

class-modifier:
	| "abstract"
	| EPSILON

inheritance-base:
	| ':' identifiers-list (1)
	| EPSILON (2)

-1 { return identifiers-list()}
-2 { return null}


class-body:
	| '{' optional-class-member-declaration-list '}'

optional-class-member-declaration-list:
	| class-member-declaration optional-class-member-declaration-list
	| EPSILON

class-member-declaration: 
	| encapsulation-modifier class-member-declaration-options

; SEMANTIC: void solo puede ir en un method. validar constructor-declaration con look_ahead
; SEMANTIC: los fields solo pueden llevar static. Y no ninguno demas de optional-modifier
class-member-declaration-options:
    | optional-modifier type-or-void identifier field-or-method
	| constructor-declaration

field-or-method:
    | field-declaration
	| method-declaration

//SEMANTIC: Constructor no puede terminar con ;
constructor-declaration:
	| constructor-declarator maybe-empty-block

constructor-declarator:
	| identifier '(' fixed-parameters ')' constructor-initializer

constructor-initializer:
	| ':' "base" '(' argument-list ')'
	| EPSILON

optional-body-end:
	| ';'
	| EPSILON

identifiers-list:
	| qualified-identifier (1) identifiers-list-p

-1 { 
	var qualified-identifier = qualified-identifier();
	var list = identifiers-list-p()
	list.insert(0, qualified-identifier)
	return list;
	}

identifiers-list-p:
	| ',' qualified-identifier (1) identifiers-list-p (2)
	| EPSILON (3)

-1 { 
		var qualified-identifier = qualified-identifier()
		var list = identifiers-list-p();
	}
-2  {
		list.insert(0,qualified-identifier)
		return list;
	}

-3  {
		return new List<qualified-identifier>()
	}


===================================================

optional-statement-list:
	| statement-list
	| EPSILON

statement-list: 
	| statement optional-statement-list 

statement:
	| local-variable-declaration ';'
	| embedded-statement

embedded-statement:
	| maybe-empty-block
	| statement-expression ';'
	| selection-statement
	| iteration-statement
	| jump-statement ';'

iteration-statement:
	| while-statement
	| do-statement
	| for-statement
	| foreach-statement

maybe-empty-block:
	| '{' optional-statement-list '}'
	| ';'

while-statement:
	| "while" '(' expression ')' embedded-statement

do-statement: 
	| "do" embedded-statement "while" '(' expression ')' ';'

for-statement:
	| "for" '(' optional-for-initializer ';' optional-expression ';' optional-statement-expression-list ')' embedded-statement

foreach-statement:
	| "foreach" '(' type-or-var identifier "in" expression ')' embedded-statement

jump-statement:
	| "break"
	| "continue"
	| "return" optional-expression

optional-statement-expression-list:
	| statement-expression-list
	| EPSILON

statement-expression-list:
	| statement-expression statement-expression-list-p

statement-expression-list-p:
	| ',' statement-expression statement-expression-list-p
	| EPSILON

optional-expression: 
	| expression
	| EPSILON

optional-for-initializer:
	| local-variable-declaration
	| statement-expression-list
	| EPSILON

selection-statement:
	| if-statement
	| switch-statement

switch-statement:
	| "switch" '(' expression ')' '{' optional-switch-section-list '}'

optional-switch-section-list:
	| switch-label-list statement-list optional-switch-section-list
	| EPSILON

switch-label-list:
	| switch-label ':' switch-label-list-p

switch-label-list-p:
	| switch-label-list
	| EPSILON
	
switch-label:
	| "case" expression
	| "default"

if-statement:
	| "if" '(' expression ')' embedded-statement optional-else-part

optional-else-part:
	| else-part
	| EPSILON

else-part:
	| "else" embedded-statement	

statement-expression:
	| unary-expression statement-expression-factorized

//nueva produccion
statement-expression-factorized:
	| assignment-operator expression statement-expression-p
	| statement-expression-p
//Borrar optional-unary-expression (innesesario)
optional-unary-expression:
    | expression-unary-operator unary-expression
    | '(' type ')'
    | EPSILON

local-variable-declaration:
	| type-or-var variable-declarator-list	

fixed-parameter:
	| (1) type (2) identifier (3)

-1  {var type = type()}

-2  {var id= new identifierNode(identifier.lexema)}

-3  {return new parameter(type, id) }


fixed-parameters:
	| (1) fixed-parameter (2) fixed-parameters-p (3)
	| EPSILON (4)

-1  {
		var parameter = fixed-parameter();
	}

-2  {
		var list = fixed-parameters-p();
	}

-3  {
		list.insert(0, parameter);
		return list;
	}
-4  {
		return new List<parameter>();
	}

fixed-parameters-p:
	| ',' (1) fixed-parameter (2) fixed-parameters-p (3)
	| EPSILON (4)
-1  {
		var parameter = fixed-parameter();
	}

-2  {
		var list = fixed-parameters-p();
	}

-3  {
		list.insert(0, parameter);
		return list;
	}
-4  {
		return new List<parameter>();
	}

type:
	| dictionary (1) optional-rank-specifier-list (2)
	| built-in-type (3) optional-rank-specifier-list (4)
	| qualified-identifier (5) optional-rank-specifier-list (6)

-1  {
		var dictionary =  dictionary();
	}

-2  {
		return new Type(dictionary, optional-rank-specifier-list())

-3  {
		var primitiveType = built-in-type()
	}
-4  {
		return new Type(primitiveType, optional-rank-specifier-list())
	}
-5  {
		var qualified-identifier = qualified-identifier();
	}

-6  {
		return new Type(qualified-identifier, optional-rank-specifier-list())
	}


//nueva
dictionary:
	| "Dictionary" '<' (1) type ',' (2) type '>' (3)

-1  {
		var type1 = type();
	}
-2  {
		var type2 = type();
	}
-3  {
		return new Dictionary(type1, type2);
	}

//nueva
built-in-type:
	| "int" { return new IntType() }
	| "char" { return new CharType() } 
	| "string" {}
	| "bool" {}
	| "float" {}
//nueva
qualified-identifier:
	| identifier (1) identifier-attribute (2)

-1  { 
		var identifier = identifier();
		var lista = identifier-attribute();
	}

-2  {
		lista.insert(0, identifier);
		return lista;
	}

type-or-var:
	| type
	| "var"

type-or-void:
	| type (1)
	| "void" (2)

-1 { return type() }
-2 { return new Type(new VoidType(),null)}

argument-list:
	| expression argument-list-p
	| EPSILON

argument-list-p:
	| ',' expression argument-list-p
	| EPSILON

===============================================================

expression:
    | conditional-expression

conditional-expression:
	| null-coalescing-expression conditional-expression-p

conditional-expression-p:
	| '?' expression ':' expression 
	| EPSILON

null-coalescing-expression:
	| conditional-or-expression null-coalescing-expression-p

null-coalescing-expression-p:
	| "??" null-coalescing-expression
	| EPSILON

conditional-or-expression:
	| conditional-and-expression conditional-or-expression-p

conditional-or-expression-p:
	| "||" conditional-and-expression conditional-or-expression-p 
	| EPSILON

conditional-and-expression:
	| inclusive-or-expression conditional-and-expression-p

conditional-and-expression-p:
	| "&&" inclusive-or-expression conditional-and-expression-p
	| EPSILON 

inclusive-or-expression:
	| exclusive-or-expression inclusive-or-expression-p

inclusive-or-expression-p:
	| "|" exclusive-or-expression inclusive-or-expression-p
	| EPSILON

exclusive-or-expression:
	| and-expression exclusive-or-expression-p

exclusive-or-expression-p:
	| "^" and-expression exclusive-or-expression-p
	| EPSILON 

and-expression:
	| equality-expression and-expression-p

and-expression-p:
	| "&" equality-expression and-expression-p
	| EPSILON

equality-expression:
	| relational-expression equality-expression-p

equality-expression-p:
	| expression-equality-operator relational-expression equality-expression-p
	| EPSILON

relational-expression:
	| shift-expression relational-expression-p

relational-expression-p:
	| expression-relational-operator shift-expression relational-expression-p
	| is-as-operators type relational-expression-p
	| EPSILON 

shift-expression:
	| additive-expression shift-expression-p

shift-expression-p:
	| expression-shift-operator additive-expression shift-expression-p
	| EPSILON

additive-expression:
	| multiplicative-expression additive-expression-p

additive-expression-p:
	| additive-operators multiplicative-expression additive-expression-p
	| EPSILON

multiplicative-expression:
	| unary-expression multiplicative-expression-factorized

multiplicative-expression-factorized:
	| assignment-operator expression multiplicative-expression-p
	| multiplicative-expression-p

multiplicative-expression-p:
	| multiplicative-operators unary-expression multiplicative-expression-p
	| EPSILON	

is-as-operators:
	| "is"
	| "as"

multiplicative-operators:
	| '*'
	| '/'
	| '%'

additive-operators:
	| '+'
	| '-'
expression-shift-operator: 
	| "<<"
	| ">>"

expression-equality-operator:
	| "=="
	| "!="

expression-relational-operator:
	| '<'
	| '>'
	| "<="
	| ">="

assignment-operator:
	| '='
	| "+="
	| "-="
	| "*="
	| "/="
	| "%="
	| "&="
	| "|="
	| "^="
	| "<<="
	| ">>="

unary-expression: //look_ahead dos veces PROGRAMATICALLY
	| expression-unary-operator unary-expression
	| '(' type ')' primary-expression
	| primary-expression

expression-unary-operator:
	| '+'
	| '-'
	| increment-decrement
	| "!"
	| "~"
	| "*"

# REVISAR 'BASE'
//SEMANTIC: validar que un literal no puede tener una llamada a
//  funcion por la llamda a primary-expression-p
primary-expression:
    | "new" instance-expression primary-expression-p
	| literal primary-expression-p
	| identifier primary-expression-p
	| "base" primary-expression-p
	| '(' expression ')' primary-expression-p
	| "this" primary-expression-p

instance-expression:
	| type instance-expression-factorized

instance-expression-factorized:
	| '[' instance-expression-factorized-p 
	| '(' argument-list ')'

instance-expression-factorized-p:
	| expression-list ']' optional-rank-specifier-list optional-array-initializer
	| rank-specifier-list array-initializer

primary-expression-p:
    | '.' identifier primary-expression-p
    | optional-funct-or-array-call primary-expression-p
    | increment-decrement primary-expression-p 
    | EPSILON

optional-funct-or-array-call:
	| '(' argument-list ')'
	| optional-array-access-list 

optional-array-access-list:
	| '[' expression-list ']' optional-array-access-list
	| EPSILON

literal:
	| int-literal
	| char-literal
	| float-literal
	| string-literal
	| "true"
	| "false"

expression-list:
	| expression expression-list-p 

expression-list-p:
	| ',' expression-list
	| EPSILON

rank-specifier-list: 
	| (1) rank-specifier  (2) optional-rank-specifier-list

-1  {
		rank-specifier(ref param.ArrayType);
	}	

-2  {
		optional-rank-specifier-list(ref param.ArrayType)
	}	

optional-rank-specifier-list:
	| '[' (1) rank-specifier-list
	| EPSILON

-1  {
		rank-specifier-list(ref Param.ArrayType);
	}

rank-specifier:
	| (1) optional-comma-list ']' (2)

-1  {
		optional-comma-list(ref param.ArrayType)
	}

-2 	{
		param.ArrayType.arrayOfArrays +=1;
	}

optional-comma-list:
	| (1) comma-list
	| EPSILON

-1  {
		comma-list(ref param.ArrayType)
	}

comma-list: 
	| ',' (1) optional-comma-list

-1  {
		param.ArrayType.dimensions++;
		optional-comma-list(ref param.ArrayType);
	}

;SEMANTIC: Validar que si la clase es abstract, el metodo no debe llevar cuerpo.
method-declaration:
	| '(' fixed-parameters ')' maybe-empty-block

interface-method-header:
	| type-or-void identifier (1) '(' fixed-parameters ')' (2)

-1  {
		var type = type-or-void()
		var method = new methodPrototype(type, identifiers)
		method.parameters = fixed-parameters();
	}

-2  {
		return method;
	}

field-declaration: 
	| variable-assigner variable-declarator-list-p ';'

variable-declarator-list:
	| identifier variable-assigner variable-declarator-list-p

variable-assigner:
	| '=' variable-initializer
	| EPSILON

variable-declarator-list-p:
	| ',' variable-declarator-list
	| EPSILON

variable-initializer:
	| expression
	| array-initializer

#SEMANTICO: Validar que static solo puede estar en field. Virtual, override y abstract, unicamente en method.
optional-modifier:
    | "static"
    | "virtual"
    | "override"
    | "abstract"
    | EPSILON

interface-declaration: 
	| "interface" identifier (1) inheritance-base (2) interface-body (3) optional-body-end

-1  {
		var interface = new Interface(identifier.lexema)
		interface.inheritance = inheritance-base();
	}

-2  {
		interface.methotds[] =  interface-body();
	}

interface-body:
	| '{' (1) interface-method-declaration-list '}'

-1 {return interface-method-declaration-list() }

interface-method-declaration-list:
	| interface-method-header ';' (1) interface-method-declaration-list (2)
	| EPSILON (3)

-1  {
		var interface-method-header = interface-method-header();
		var list = interface-method-declaration-list()
	}

-2  {
		list.insert(0,interface-method-header)
		return list;
	}

-3 	{
		return new List<interface-method-declarationNode>
	}

enum-declaration: #DONE
	| "enum" identifier enum-body (1) optional-body-end (2)

-1 {new enum-definition-node(enum-declaration.param, identifier).enum-node[] = enum-body()}
-2 {enum-declaration.valor = enum-definition-node}

enum-body:#DONE
	| '{' optional-assignable-identifiers-list '}' (1)

-1  {enum-body.valor = optional-assignable-identifiers-list(new enum-node[]).valor}

optional-assignable-identifiers-list:#DONE
	| identifier (1) assignment-options (2)
	| EPSILON (3)

-1 {assignment-options(new identifier-node(identifier.lexema), ref optional-assignable-identifiers-list.param)}
-3 {return optional-assignable-identifiers-list.param}

assignment-options:#DONE
	| optional-assignable-identifiers-list-p (1)
	| '=' expression optional-assignable-identifiers-list-p (2)

-1 { optional-assignable-identifiers-list-p(assignment-options.param1, ref assignment-options.param2, null) }
-2 { optional-assignable-identifiers-list-p(assignment-options.param1, ref assignment-options.param2, expresion.valor}

optional-assignable-identifiers-list-p: #DONE
	| ',' (1) optional-assignable-identifiers-list
	| EPSILON (2)

	//param1 = identifier-node
	//param2 = enum-node[]
	//param3 = enum.valor

-1  {	optional-assignable-identifiers-list-p.param2.add(new enum-Node(param1,param3))
		optional-assignable-identifiers-list(param2)
	}

-2  {	
		optional-assignable-identifiers-list-p.param2.add(new enum-Node(param1,param3))
	}
optional-array-initializer:
	| array-initializer
	| EPSILON

array-initializer: 
	| '{' optional-variable-initializer-list '}'

optional-variable-initializer-list:
	| variable-initializer-list
	| EPSILON

variable-initializer-list:
	| variable-initializer variable-initializer-list-p

variable-initializer-list-p:
	| ',' variable-initializer-list
	| EPSILON

increment-decrement:
	| "++"
	| "--"

statement-expression-p:
	| '(' argument-list ')'
	| increment-decrement
    | EPSILON